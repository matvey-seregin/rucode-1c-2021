
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()
	
	ШаблонHTML = Обработки.БиржеваяДиаграммаПитания.ПолучитьМакет("ШаблонВебСтраницы").ПолучитьТекст();
	
	ШаблонHTML = СтрЗаменить(ШаблонHTML, "[[ДанныеДиаграммы]]", ДанныеДиаграммыJSON());
	
	ДанныеHTML = ШаблонHTML;
	
КонецПроцедуры

&НаСервере
Функция ДанныеДиаграммыJSON()
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НАЧАЛОПЕРИОДА(ЗатратыНаПитаниеОбороты.Период, ДЕНЬ) КАК Период,
	               |	ЗатратыНаПитаниеОбороты.СуммаОборот КАК Сумма
	               |ИЗ
	               |	РегистрНакопления.ЗатратыНаПитание.Обороты(, , Регистратор, ) КАК ЗатратыНаПитаниеОбороты
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Сумма
	               |ИТОГИ ПО
	               |	Период";
	
	ВыборкаПериоды = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПериоды.Следующий() Цикл
		
		ВыборкаСуммы = ВыборкаПериоды.Выбрать();
		
		Суммы = Новый Массив;
		
		Пока ВыборкаСуммы.Следующий() Цикл
			Суммы.Добавить(ВыборкаСуммы.Сумма);
		КонецЦикла;
		
		ДанныеПериода = Новый Структура;
		ДанныеПериода.Вставить("date", Формат(ВыборкаПериоды.Период, "ДФ=гггг-ММ-дд"));
		ДанныеПериода.Вставить("high", Суммы[Суммы.ВГраница()]);
		ДанныеПериода.Вставить("low", Суммы[0]);
		ДанныеПериода.Вставить("open", ЗначениеОткрытия(Суммы));
		ДанныеПериода.Вставить("close", ЗначениеЗакрытия(Суммы));
		
		Результат.Добавить(ДанныеПериода);
		
	КонецЦикла;
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, Результат);
	
	СтрокаJSON = Запись.Закрыть();
	Возврат СтрокаJSON;	
	
КонецФункции
	
Функция ЗначениеОткрытия(Суммы)
	
	ПозицияОткрытия = Цел(Суммы.ВГраница() / 4);
	
	Возврат Суммы[ПозицияОткрытия];
	
КонецФункции
	
Функция ЗначениеЗакрытия(Суммы)
	
	ПозицияОткрытия = Цел(Суммы.ВГраница() * 3 / 4);
	
	Возврат Суммы[ПозицияОткрытия];
	
КонецФункции


